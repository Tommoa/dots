if not test -d $HOME/.config/ion/plugins
	# We don't have plugins, so clone them in first.
	git clone https://gitlab.redox-os.org/redox-os/ion-plugins $HOME/.config/ion/plugins
end

source ~/.config/ion/plugins/init.ion
source ~/.config/ion/plugins/tools/gpg-agent.ion

export SKIM_DEFAULT_OPTIONS="--multi --ansi --bind=ctrl-e:up --preview='bat --plain --color=always {}'"

if exists -s SSH_CLIENT
	# We're in an SSH session

	# Set a custom prompt
	fn PROMPT
		printf "${c::0x55,bold}${USER}${c::red}\@${c::black}${HOST}${c::default}:${c::0x4B}${SWD}${c::default}# ${c::reset}"
	end
else
	# Only launch TMUX if we're not in an SSH session
	if not exists -s TMUX
		let tmux = $(/usr/bin/env which tmux)
		$tmux attach
		if not eq $? 0
			$tmux new-session
		end
		exit
	end
end

fn update_rust
	let gitdr = $(git fetch --dry-run &| wc -l)
	let gitdr:int = $((gitdr))
	if test $(echo $gitdr) -ne 0
		echo "${c::red}Updating${c::reset}"
		git checkout .
		git pull --recurse-submodules
		cargo install --path . --force
	else
		echo "${c::green}Already updated!${c::reset}"
	end
end

export EDITOR=nvim

if matches $(uname) "Darwin"
	# We're on OSX
	# Set path to use homebrew's less
	export PATH=/usr/local/bin:$PATH:/usr/local/texlive/2019/bin/x86_64-darwin
	export PAGER=less
end
source ~/.cargo/env
